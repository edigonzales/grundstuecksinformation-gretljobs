import ch.so.agi.gretl.api.TransferSet
import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "ch.so.agi.gretl"

defaultTasks "dbImport"

// Download-Verzeichnis
def pathToTempFolder = System.getProperty("java.io.tmpdir")

// Oereb-DB Schemas
//def dbSchemas = ["stage", "live"]
def dbSchemas = ["live"]

def GROUP = "Bundesgesetze-Import"

// Bundesgesetze importieren (nur bei Bedarf)
task download(type: Download) {
    group = GROUP
    description = "Download Bundesdaten"
    src "http://models.geo.admin.ch/V_D/OeREB/OeREBKRM_V1_1_Gesetze_20180501.xml"
    dest pathToTempFolder
    overwrite true
}

dbSchemas.each { dbSchema ->
    task "import_$dbSchema"(type: Ili2pgReplace, dependsOn: "download") {
        group = GROUP
        description = "Import Bundesdaten Gesetze in DB agi_oereb"
        database = [dbUriGrundstuecksinformation, dbUserGrundstuecksinformation, dbPwdGrundstuecksinformation]
        dbschema = dbSchema
        dataset = "OeREBKRM_V1_1_Gesetze"
        models = "OeREBKRMvs_V1_1"
        disableValidation = false
        dataFile = file(Paths.get(pathToTempFolder.toString(), "OeREBKRM_V1_1_Gesetze_20180501.xml"))
    }
}

task dbImport() {
    group = GROUP
    description = "Aggregation task."
    dependsOn {
        tasks.findAll { task -> task.name.startsWith('import_') }
    }    
    
    doLast {
        println "Data import into db finished."
    }
}